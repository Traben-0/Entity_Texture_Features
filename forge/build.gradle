plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

archivesBaseName = rootProject.archives_base_name + "_forge_1.20.3"

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
        mixinConfig "entity_texture_features-forge.mixins.json"
        mixinConfig "entity_texture_features-common.mixins.json"

    }
}


configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    //modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"

    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    implementation(forgeDependencies("org.apache.httpcomponents:httpmime:4.5.10"))
    shadow('org.apache.httpcomponents:httpmime:4.5.14')


    modImplementation("maven.modrinth:3dskinlayers:${project.skinlayers_version_forge}") {
        //exclude(group: "net.fabricmc.fabric-api")
    }

    include group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.10'
    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.10'
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
//    classifier "dev-shadow"
    archiveClassifier.set('dev-shadow')
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
//    classifier null
    archiveClassifier.set(null)
}

jar {
//    classifier "dev"
    archiveClassifier.set('dev')
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

repositories {
    mavenCentral()
}
